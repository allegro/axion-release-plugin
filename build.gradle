plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven'
    id 'jacoco'
    id 'pl.allegro.tech.build.axion-release' version '1.4.1'
    id 'com.github.kt3k.coveralls' version '2.3.1'
    id "com.gradle.plugin-publish" version "0.9.1"
    id "com.bmuschko.nexus" version "2.3.1" apply false
    id "io.codearte.nexus-staging" version "0.5.3" apply false
    id 'maven-publish'
}

if (project.hasProperty('mavenCentral')) {
    apply from: 'gradle/mavenCentral.gradle'
}

scmVersion {
    tag {
        prefix = 'axion-release'
    }

    hooks {
        pre 'fileUpdate', [files: ['README.md', 'docs/conf.py'], pattern: { v, p -> /'$v'/ }, replacement: { v, p -> "'$v'" }]
        pre 'commit'
    }

    versionCreator 'versionWithBranch'
}

group = 'pl.allegro.tech.build'
version = scmVersion.version

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

project.ext.versions = [
        jgit: '4.1.1.201511131810-r'
]

sourceSets {
    integration {
        java.srcDir project.file('src/integration/java')
        groovy.srcDir project.file('src/integration/groovy')

        resources.srcDir project.file('src/integration/resources')
        resources.srcDir project.sourceSets.test.resources
        resources.srcDir project.sourceSets.main.resources

        compileClasspath = project.sourceSets.main.output +
                project.sourceSets.test.output +
                project.configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'org.ajoberstar', name: 'grgit', version: '1.4.1'

    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: versions.jgit
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.ui', version: versions.jgit

    compile group: 'com.jcraft', name: 'jsch', version: '0.1.53'
    compile group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'

    testCompile(group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.1'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '2.4'

    testCompile gradleTestKit()
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

project.configurations {
    integration {
        extendsFrom project.configurations.testRuntime
        description = 'Dependencies for integration tests'
        transitive = true
        visible = true
    }
}

task integrationTest(type: Test) {
    testClassesDir = project.sourceSets.integration.output.classesDir
    classpath = project.sourceSets.main.output +
            project.sourceSets.test.output +
            project.sourceSets.integration.runtimeClasspath +
            project.configurations.testRuntime +
            project.configurations.integrationRuntime

    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

tasks.check.dependsOn integrationTest

gradlePlugin {
    testSourceSets project.sourceSets.integration
		plugins {
			release {
				id = "pl.allegro.tech.build.axion-release"
				implementationClass = "pl.allegro.tech.build.axion.release.ReleasePlugin"
			}
		}
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}

//pluginBundle {
//    website = 'https://github.com/allegro/axion-release-plugin'
//    vcsUrl = 'https://github.com/allegro/axion-release-plugin'
//    description = 'Release and version management plugin.'
//    tags = ['release', 'version']
//
//    plugins {
//        release {
//            id = 'pl.allegro.tech.build.axion-release'
//            displayName = 'axion-release-plugin'
//        }
//    }
//
//    mavenCoordinates {
//        groupId = 'pl.allegro.tech.build'
//        artifactId = 'axion-release-plugin'
//    }
//}

publishing {
	repositories {
		maven {
			url uri('file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath)
		}
	}
}
