import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'pl.allegro.tech.build.axion-release' version '1.13.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
    id 'com.coditory.integration-test' version "1.0.5"
    id 'com.adarshr.test-logger' version '2.1.1'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.bmuschko.docker-remote-api' version '3.2.1'
}

scmVersion {
    tag {
        prefix = 'axion-release-'
    }

    hooks {
        pre 'fileUpdate', [files: ['README.md', 'mkdocs.yml'], pattern: { v, p -> /'$v'/ }, replacement: { v, p -> "'$v'" }]
        pre 'commit'
    }

    versionCreator 'versionWithBranch'
}

group = 'pl.allegro.tech.build'
version = scmVersion.version

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    jcenter()
}

project.ext.versions = [
        jgit: '5.10.0.202012080955-r',
        jsch: '0.1.55',
        jschAgent: '0.0.9'
]

sourceSets {
    main {
        java { srcDirs = [] }    // no source dirs for the java compiler
        groovy { srcDirs = ["src/main/java", "src/main/groovy"] }  // compile   everything in src/ with groovy
    }

    remoteTest {
        java.srcDir project.file('src/test-remote/java')
        groovy.srcDir project.file('src/test-remote/groovy')

        resources.srcDir project.file('src/test-remote/resources')
        resources.srcDir project.sourceSets.test.resources
        resources.srcDir project.sourceSets.main.resources

        compileClasspath = project.sourceSets.main.output +
            project.sourceSets.test.output +
            project.configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

dependencies {
    shadow gradleApi()
    shadow localGroovy()

    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: versions.jgit
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.ssh.jsch', version: versions.jgit
    runtimeOnly group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.ui', version: versions.jgit
    runtimeOnly group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.gpg.bc', version: versions.jgit

    implementation group: 'com.jcraft', name: 'jsch', version: versions.jsch
    implementation group: 'com.jcraft', name: 'jsch.agentproxy.core', version: versions.jschAgent
    implementation group: 'com.jcraft', name: 'jsch.agentproxy.jsch', version: versions.jschAgent
    implementation group: 'com.jcraft', name: 'jsch.agentproxy.sshagent', version: versions.jschAgent
    implementation group: 'com.jcraft', name: 'jsch.agentproxy.pageant', version: versions.jschAgent
    implementation group: 'com.jcraft', name: 'jsch.agentproxy.usocket-jna', version: versions.jschAgent
    implementation group: 'com.jcraft', name: 'jsch.agentproxy.usocket-nc', version: versions.jschAgent

    implementation group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'

    testCompile (group: 'org.ajoberstar.grgit', name: 'grgit-core', version: '4.0.1') {
        exclude group: 'org.eclipse.jgit', module: 'org.eclipse.jgit.ui'
        exclude group: 'org.eclipse.jgit', module: 'org.eclipse.jgit'
    }

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.1'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '2.4'
    testCompile group: 'org.apache.sshd', name: 'sshd-core', version: '1.6.0'
    testCompile group: 'org.apache.sshd', name: 'sshd-git', version: '1.6.0'

    testCompile gradleTestKit()
}

shadowJar {
    minimize()
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar


shadowJar {
    archiveClassifier.set('')
}

project.configurations {
    remoteTest {
        extendsFrom project.configurations.testRuntime
        description = 'Dependencies for tests with Docker'
        transitive = true
        visible = true
    }
}

task buildDockerImage(type: DockerBuildImage) {
    inputDir = file('docker')
    tag = 'test/axion-release-remote:latest'
}

task createDockerContainer(type: DockerCreateContainer) {
    dependsOn buildDockerImage
    targetImageId { buildDockerImage.getImageId() }
    portBindings = ['2222:22']
}

task startDockerContainer(type: DockerStartContainer) {
    dependsOn createDockerContainer
    targetContainerId { createDockerContainer.getContainerId() }
}

task stopDockerContainer(type: DockerStopContainer) {
    targetContainerId { createDockerContainer.getContainerId() }
}

task remoteTest(type: Test) {
    testClassesDirs = project.sourceSets.remoteTest.output.classesDirs
    classpath = project.sourceSets.main.output +
        project.sourceSets.test.output +
        project.sourceSets.remoteTest.runtimeClasspath +
        project.configurations.testRuntime +
        project.configurations.remoteTestRuntime

    dependsOn startDockerContainer
    finalizedBy stopDockerContainer
}

tasks.check.dependsOn integrationTest, remoteTest

gradlePlugin {
    testSourceSets project.sourceSets.integration
}

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

pluginBundle {
    website = 'https://github.com/allegro/axion-release-plugin'
    vcsUrl = 'https://github.com/allegro/axion-release-plugin'
    description = 'Release and version management plugin.'
    tags = ['release', 'version']

    plugins {
        release {
            id = 'pl.allegro.tech.build.axion-release'
            displayName = 'axion-release-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'pl.allegro.tech.build'
        artifactId = 'axion-release-plugin'
    }
}

publishing {
    publications {
        sonatype(MavenPublication) { publication ->
            from project.shadow.component(publication)
            pom {
                name = project.name
                description = 'Gradle release and version management plugin'
                url = 'https://github.com/allegro/axion-release-plugin'
                inceptionYear = '2014'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'adamdubiel'
                        name = 'Adam Dubiel'
                    }
                }
                scm {
                    url = 'https://github.com/allegro/axion-release-plugin'
                    connection = 'scm:git@github.com:allegro/axion-release-plugin.git'
                    developerConnection = 'scm:git@github.com:allegro/axion-release-plugin.git'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

if (System.getenv("GPG_KEY_ID")) {
    signing {
        useInMemoryPgpKeys(
            System.getenv("GPG_KEY_ID"),
            System.getenv("GPG_PRIVATE_KEY"),
            System.getenv("GPG_PRIVATE_KEY_PASSWORD")
        )
        sign publishing.publications.sonatype
    }
}
