buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/'  }
    }
    dependencies {
        classpath group: 'com.gradle.publish', name: 'plugin-publish-plugin', version: '0.9.1'
        classpath group: 'io.codearte.gradle.nexus', name: 'gradle-nexus-staging-plugin', version: '0.5.3'
        classpath group: 'com.bmuschko', name: 'gradle-nexus-plugin', version: '2.3.1'
    }
}

plugins {
    id 'groovy'
    id 'maven'
    id 'jacoco'
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
    id 'com.github.kt3k.coveralls' version '2.3.1'
}

apply plugin: 'com.gradle.plugin-publish'

if(project.hasProperty('mavenCentral')) {
    apply from: 'gradle/mavenCentral.gradle'
}

scmVersion {
    tag {
        prefix = 'axion-release'
    }
    
    hooks {
        pre 'fileUpdate', [files: ['README.md', 'docs/conf.py'], pattern: {v, p -> /'$v'/ }, replacement: {v, p -> "'$v'"}]
        pre 'commit'
    }

    versionCreator 'versionWithBranch'
}

group = 'pl.allegro.tech.build'
version = scmVersion.version

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

project.ext.versions = [
        jgit: '4.3.0.201604071810-r'
]

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'org.ajoberstar', name: 'grgit', version: '1.6.0'

    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: versions.jgit
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.ui', version: versions.jgit

    compile group: 'com.jcraft', name: 'jsch', version: '0.1.53'
    compile group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'

    testCompile(group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.1'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

pluginBundle {
    website = 'https://github.com/allegro/axion-release-plugin'
    vcsUrl = 'https://github.com/allegro/axion-release-plugin'
    description = 'Release and version management plugin.'
    tags = ['release', 'version']

    plugins {
        release {
            id = 'pl.allegro.tech.build.axion-release'
            displayName = 'axion-release-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'pl.allegro.tech.build'
        artifactId = 'axion-release-plugin'
    }
}
